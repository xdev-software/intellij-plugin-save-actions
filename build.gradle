plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'org.jetbrains.intellij' version '1.17.2'
    id 'org.sonarqube' version '4.4.1.3373'
}

ext {
    checkstyleVersion = '10.14.1'
}

def properties(String key) {
    return providers.gradleProperty(key)
}

def environment(String key) {
    return providers.environmentVariable(key)
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'checkstyle'

// Add plugin group and version
group = properties("pluginGroup").get()
version = properties("pluginVersion").get()

// Add build script repository to maven central
repositories {
    mavenCentral()
}

// https://github.com/gradle/gradle/issues/27035
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}

// Add dependencies to test, junit5 api (annotations) and engine (runtime)
dependencies {
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
    testImplementation platform('org.junit:junit-bom:5.10.2'),
            'org.junit.jupiter:junit-jupiter',
            'org.junit.jupiter:junit-jupiter-engine',
            'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because("Only needed to run tests in a version of IntelliJ IDEA that bundles older versions")
    }
}

// Add intellij task configuration for base intellij version (minimum compatibility)
// This needs to fit the tag <idea-version since-build="xxx"> in plugin.xml
// See https://www.jetbrains.com/intellij-repository/snapshots
// See https://www.jetbrains.com/intellij-repository/releases
intellij {
    pluginName = properties("pluginName")

    version = properties("platformVersion")
    type = properties("platformType")

    // Plugin Dependencies. Uses `platformPlugins` property from the gradle.properties file.
    plugins = properties("platformPlugins").map { it.split(",").collect { it.trim() }.findAll { !it.empty } }

    updateSinceUntilBuild = false
}


listProductsReleases {
    sinceBuild = "223.*"
}

checkstyle {
    configDirectory = file("$rootProject.projectDir/.config/checkstyle")
    ignoreFailures false
    showViolations true
    toolVersion = checkstyleVersion
}

tasks.withType(Checkstyle).configureEach {
    enabled = project.hasProperty("checkstyleEnabled");
}

// Configure compileJava AND compileTestJava
// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.JavaCompile.html
tasks.withType(JavaCompile).configureEach {
    // JAVA compatibility
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
compileTestJava.options.encoding = 'UTF-8'

// Activate junit since gradle 4.7
test {
    useJUnitPlatform()
}

// Add resources directory because intellij test framework checks there for test resources (instead of build/resources)
sourceSets {
    test.output.resourcesDir = "build/classes/java/resources"
}

tasks {
    wrapper {
        gradleVersion = properties("gradleVersion").get()
    }

    patchPluginXml {
        version = properties("pluginVersion")
    }

    signPlugin {
        certificateChain = environment("CERTIFICATE_CHAIN")
        privateKey = environment("PRIVATE_KEY")
        password = environment("PRIVATE_KEY_PASSWORD")
    }

    publishPlugin {
        token = environment("PUBLISH_TOKEN")
        // The pluginVersion is based on the SemVer (https://semver.org) and supports pre-release labels, like 2.1.7-alpha.3
        // Specify pre-release label to publish the plugin in a custom Release Channel automatically. Read more:
        // https://plugins.jetbrains.com/docs/intellij/deployment.html#specifying-a-release-channel
        channels = properties("pluginVersion")
                .map { [((it.split('-') as List)[1] ?: "default").split('\\.').find { true }.toLowerCase()] }
    }
}
